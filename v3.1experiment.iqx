----- Some default settings -----
<defaults>
/ screencolor = black
</defaults>

----- Loading Pictures as Items and Setting Text Elements -----

<item AllFlowers>
/ 1 = "Blossom_0.2.jpg"
/ 2 = "Blossom_0.6.jpg"
/ 3 = "Blossom_1.jpg"
/ 4 = "Blossom_1.4.jpg"
/ 5 = "Blossom_1.8.jpg"
/ 6 = "Purple_0.2.jpg"
/ 7 = "Purple_0.6.jpg"
/ 8 = "Purple_1.jpg"
/ 9 = "Purple_1.4.jpg"
/ 10 = "Purple_1.8.jpg"
/ 11 = "TwoR_0.2.jpg"
/ 12 = "TwoR_0.6.jpg"
/ 13 = "TwoR_1.jpg"
/ 14 = "TwoR_1.4.jpg"
/ 15 = "TwoR_1.8.jpg"
/ 16 = "TwoY_0.2.jpg"
/ 17 = "TwoY_0.6.jpg"
/ 18 = "TwoY_1.jpg"
/ 19 = "TwoY_1.4.jpg"
/ 20 = "TwoY_1.8.jpg"
/ 21 = "White_0.2.jpg"
/ 22 = "White_0.6.jpg"
/ 23 = "White_1.jpg"
/ 24 = "White_1.4.jpg"
/ 25 = "White_1.8.jpg"
</item>

<item PracticeFlower>
/ 1 = "EZ032_L.jpg" // random flower to use as practice, no data will be recorded
</item>

<item Real_Practiceflower >
/ 1 = "EZ099_L.jpg"
</item>

<text summary>
/ items = ("Thank you for your participation. Press SPACE BAR to end the test")
/ position = (50%, 50%)
/ color = orange
/ txbgcolor = black
</text>

<text instructions>
/ items = ("Welcome to the reality test. 
	The purpose of this experiment is to evaluate the quality of different cameras. 
	A series of flowers (and other pictures) taken by different cameras will be presented on the screen, and your task is to evaluate how real each picture looks. 
	Before the real trial, you will be presented with 1 practice trial. 
	
	Press the SPACE BAR to begin practice
	
	Press CTRL + Q anytime throughout the quiz to quit")
/ hjustify = center
/ size = (90%, 60%)
/ position = (50%, 85%)
/ valign = bottom
/ select = instructions
/ fontstyle = ("Arial", 3.5%)
/ color = orange
/ txbgcolor = black
</text>


<text PracticeText>
/items = ("At the beginning of the trial, a picture will flash briefly (similar to that shown below). 
	Immediately after, you will be asked to rate its realness. 
	
	Press SPACE BAR to continue")
/ hjustify = center
/ size = (90%, 60%)
/ position = (50%, 40%)
/ select = instructions
/ fontstyle = ("Arial", 3.5%)
/ color = orange
/ txbgcolor = black
</text>

<text PostPracticeText>
/ items = ("You have now completed the practice test. Press SPACE BAR to begin the real test")
/ hjustify = center
/ size = (90%, 60%)
/ position = (50%, 85%)
/ valign = bottom
/ select = instructions
/ fontstyle = ("Arial", 3.5%)
/ color = orange
/ txbgcolor = black
</text>

<picture flowers>
/ items = AllFlowers
/ size = (50%, 50%)
</picture>

<picture Practiceflower>
/items = PracticeFlower
/ select =  sequence
/ size = (50%, 50%)
/ position = (50%, 60%)
</picture>

<picture Real_Practiceflower>
/ items = Real_Practiceflower
/ size = (50%,50%)
/ position = (50%, 50%)
</picture>


<text CatchText1>
/ items = ("Please Press the Number 3 to Continue") // Catch text, used for the Catch Trial. Here, the user must select 3 for their responses to be valid (can still proceed with other numbers, but they will be wrong!)
/ fontstyle = ("Arial", 2.5%)
/ txbgcolor = black
/ position = (50%, 20%)
/ color = orange
</text>

<text CatchText2>
/ items = ("Please Press the Number -1 to Continue") // Catch text, used for the Catch Trial. Here, the user must select -1 for their responses to be valid (can still proceed with other numbers, but they will be wrong!)
/ fontstyle = ("Arial", 2.5%)
/ txbgcolor = black
/ position = (50%, 20%)
/ color = orange
</text>

----- Trial Counter ----
<values Trial_Number>
/ TrialCounter = 0
</values>


----- Setting Up Instruction, Practice and Catch Trials -----

<trial Instruct>
/ stimulusframes = [1 = instructions]
/ correctresponse = (" ")
/ posttrialpause = 300
</trial>

<trial Practice1>
/ stimulusframes = [1 = PracticeText; 2 = Practiceflower]
/ correctresponse = (" ")
/ posttrialpause = 300

</trial>

<trial PostPractice>
/ stimulusframes = [1 = PostPracticeText]
/ correctresponse = (" ")
/ posttrialpause = 300
</trial>


<trial PracticeFlower>
/ stimulusframes = [1 = Real_Practiceflower]
/ timeout = 1000
/ posttrialpause = 300
</trial>

<trial FLOWER>
/ stimulusframes = [1 = flowers;]
/ timeout = 1000
/ posttrialpause = 300
</trial>

<trial summary>
/ stimulusframes = [1 = summary]
/ correctresponse = (" ")
</trial>


-----Drop Down Options for DPDR Test-----
NOTE: At this stage, The frequency and duration dropdown boxes need to be changed for every question (i.e. 54 separate dropdowns for the whole quiz)
this is obviously too large, but I am not sure how else to organise it. I will think about it more. 

<dropdown frequency1 >
/ caption = "Frequency: Out of the blue, I feel strange, as if I were not real or as if I were cut off from the world"
/ options = ("Never","Rarely","Often","Very Often","All the Time")
/ optionvalues = ("0","1","2","3","4")
/ txcolor = orange

</dropdown>

<dropdown duration1>
/ caption = "Duration: Out of the blue, I feel strange, as if I were not real or as if I were cut off from the world"
/ options = ("Not Applicable","A Few Seconds", "A Few Minutes","A Few Hours","About a Day","More Than a Day", "More Than a Week")
/ optionvalues = ("0","1","2","3","4","5","6")
/ txcolor = orange
</dropdown>

<dropdown frequency2 >
/ caption = "Frequency: What I see looks 'flat' or 'lifeless', as if I were looking at a picture"
/ options = ("Never","Rarely","Often","Very Often","All the Time")
/ optionvalues = ("0","1","2","3","4")
/ txcolor = orange
</dropdown>

<dropdown duration2>
/ caption = "Duration: What I see looks 'flat' or 'lifeless', as if I were looking at a picture"
/ options = ("Not Applicable","A Few Seconds", "A Few Minutes","A Few Hours","About a Day","More Than a Day", "More Than a Week")
/ optionvalues = ("0","1","2","3","4","5","6")
/ txcolor = orange
</dropdown>

<dropdown frequency3 >
/ caption = "Frequency: Parts of my body feel as if they didn't belong to me"
/ options = ("Never","Rarely","Often","Very Often","All the Time")
/ optionvalues = ("0","1","2","3","4")
/ txcolor = orange
</dropdown>

<dropdown duration3>
/ caption = "Duration: Parts of my body feel as if they didn't belong to me"
/ options = ("Not Applicable","A Few Seconds", "A Few Minutes","A Few Hours","About a Day","More Than a Day", "More Than a Week")
/ optionvalues = ("0","1","2","3","4","5","6")
/ txcolor = orange
</dropdown>

<surveypage DPDR>
/ caption = "Please answer the following items to the best of your ability"
/ questions = [1=dropdown.frequency1,dropdown.duration1; 2 = dropdown.frequency2, dropdown.duration2; 3 = dropdown.frequency3, dropdown.duration3]
/ txcolor = orange
</surveypage>


----- Circular Survey Reponse from Ariel's Resources (loading pictures and text) -----

Each of these pictures is one octant of an "octagon" of numbers which range from -4 to 4 (anticlockwise) and is covered by a 
black box in the middle. When the user clicks a number in one of the octants, another shape comes up which is clickable, 
to proceed to the next stage 

<picture response1d>
/ items = ("response1disks.png") // Corresponds to the number 4
/ position = (55.7%,40%)
/ size = (15.8%,15.8%)
</picture>

<picture response2d>
/ items = ("response2disks.png") // Corresponds to the number 3 
/ position = (55.7%,40%)
/ size = (15.8%,15.8%)
</picture>

<picture response3d>
/ items = ("response3disks.png") // Corresponds to the number 2
/ position = (55.7%,60%)
/ size = (15.8%,15.8%)
</picture>

<picture response4d>
/ items = ("response4disks.png") // Corresponds to the number 1 
/ position = (55.7%,60%)
/ size = (15.8%,15.8%)
</picture>

<picture response5d>
/ items = ("response5disks.png") // Corresponds to the number -1
/ position = (44.3%,60%)
/ size = (15.8%,15.8%)
</picture>

<picture response6d>
/ items = ("response6disks.png") // Corresponds to the number -2 
/ position = (44.3%,60%)
/ size = (15.8%,15.8%)
</picture>

<picture response7d>
/ items = ("response7disks.png") // Corresponds to the number -3
/ position = (44.3%,40%)
/ size = (15.8%,15.8%)
</picture>

<picture response8d>
/ items = ("response8disks.png") // Corresponds to the number -4
/ position = (44.3%,40%)
/ size = (15.8%,15.8%)
</picture>

<picture response_blank>
/ items = ("response_blank.png") // Corresponds to the black square 
/ position = (50%,50%)
/ size = (12%,12%)
</picture>

<shape orangesquare>
/ size = (8%, 8%)
/ position = (50%, 50%) // Corresponds to the orange square present after mouse click on an octant
/ shape = rectangle
/ color = orange
</shape>

<text unreal>
/items = ("Unreal")
/ fontstyle = ("Arial",5%)
/ position = (30%,50%)
/ color = aqua
/ txbgcolor = black
</text>

<text real>
/ items = ("Real")
/ fontstyle = ("Arial",5%)
/ position = (70%, 50%)
/ color = aqua
/ txbgcolor = black
</text>

<text RealityReminder>
/ items = ("Please choose the reality level of previous stimulus
		-4 => Most Unreal
		4 => Most Real")
/ fontstyle = ("Arial", 2.5%)
/ txbgcolor = black
/ position = (50%, 80%)
/ color = aqua
</text>

<text ContinueText>
/ items = ("Please press the orange box to continue")
/ fontstyle = ("Arial", 2.5%)
/ txbgcolor = black
/ position = (50%, 20%)
/ color = orange
</text>

<text QuestionNumberReminder>
/ items = ("You have finished <%values.TrialCounter%> of 27 Questions")
/ fontstyle = ("Arial", 2.5%)
/ txbgcolor = black
/ position = (50%, 90%)
/ color = aquamarine
</text>



----- Practice Trials -----

<trial peripheral_response_practice>
/ stimulusframes = [1  = response1d, response2d, response3d, response4d, response5d, response6d, response7d, response8d, response_blank, real, unreal, RealityReminder]
/ validresponse = (response1d, response2d, response3d, response4d, response5d, response6d, response7d, response8d)
/ inputdevice = mouse
/ branch = [
	trial.centermouse_practice
]
</trial>

<trial centermouse_practice>
/ stimulusframes = [1  = response1d, response2d, response3d, response4d, response5d, response6d, response7d, response8d, orangesquare, real, unreal, RealityReminder, ContinueText]
/ inputdevice = mouse
/ recorddata = false
/ validresponse = (orangesquare)
</trial>

----- Actual Trials -----


<trial peripheral_response>
/ stimulusframes = [1  = response1d, response2d, response3d, response4d, response5d, response6d, response7d, response8d, response_blank, real, unreal, RealityReminder, QuestionNumberReminder]
/ validresponse = (response1d, response2d, response3d, response4d, response5d, response6d, response7d, response8d)
/ inputdevice = mouse
/ branch = [
	trial.centremouse
]
</trial>

<trial centremouse>
/ stimulusframes = [1  = response1d, response2d, response3d, response4d, response5d, response6d, response7d, response8d, orangesquare, real, unreal, RealityReminder, ContinueText, QuestionNumberReminder]
/ inputdevice = mouse
/ recorddata = false
/ validresponse = (orangesquare)
/ ontrialend = [
	values.TrialCounter += 1
]
</trial>


----- Other Trials -----

<trial EndPracticeTrial>
/ stimulusframes = [1 = PracticeText]
/ timeout = 20000
</trial>

<trial CatchTrial1>
/ stimulusframes = [1  = response1d, response2d, response3d, response4d, response5d, response6d, response7d, response8d, response_blank, real, unreal, RealityReminder, CatchText1, QuestionNumberReminder]
/ validresponse = (response1d, response2d, response3d, response4d, response5d, response6d, response7d, response8d)
/ inputdevice = mouse
/ branch = [
	trial.centremouse
]
</trial>

<trial CatchTrial2>
/ stimulusframes = [1  = response1d, response2d, response3d, response4d, response5d, response6d, response7d, response8d, response_blank, real, unreal, RealityReminder, CatchText2, QuestionNumberReminder]
/ validresponse = (response1d, response2d, response3d, response4d, response5d, response6d, response7d, response8d)
/ inputdevice = mouse
/ branch = [
	trial.centremouse
]
</trial>

----- Final Block -----
<block test>
/ trials = [1=Instruct; 2 = Practice1; 3 = PracticeFlower; 4 = peripheral_response_practice; 5 = PostPractice; 6,8,10,12,14,16,18,20,22,24,26,28,30,32,34,36,38,40,42,44,46,48,50,52,54 = noreplace(FLOWER); 7,9,11,13,15,17,19,21,23 = peripheral_response; 
25 = CatchTrial1; 27,29,31,33,35,37,39,41,43,45,47 = peripheral_response; 49 = CatchTrial2; 51,53,55 = peripheral_response; 56 = surveypage.DPDR; 57 = summary]
</block>

